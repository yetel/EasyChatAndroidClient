apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

//使用生成文件
kapt {
    generateStubs = true
}

androidExtensions {
    experimental = true
}

kotlin{
    experimental {
        coroutines 'enable'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.king.easychat"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi-v7a"//, "arm64-v8a", "x86","x86_64"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    if(rootProject.ext.signingEnabled){
        signingConfigs {
            config  {
                //加载配置资源
                Properties properties = new Properties()
                InputStream inputStream = rootProject.file('local.properties').newDataInputStream()
                properties.load( inputStream )

                //读取配置文件
                def storeFileDir = properties.getProperty('storeFile.file')
                storeFile file( storeFileDir )

                //读取配置字段
                def key_keyAlias = properties.getProperty('keyAlias')
                def key_keyPassword = properties.getProperty('keyPassword')
                def key_storePassword = properties.getProperty('storePassword')

                keyAlias key_keyAlias
                keyPassword key_keyPassword
                storePassword key_storePassword
            }

        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (rootProject.ext.signingEnabled) {
                signingConfig signingConfigs.config
            }
        }

        debug {
            debuggable true
            minifyEnabled false
        }
    }

    def API_RELEASE_HOST = "\"http://xx.xx.xx:9000/\""
    def API_DEV_HOST = "\"http://xx.xx.xx:9000/\""

    flavorDimensions "flavor"
    productFlavors {
        //正式
        flavors_release {
            dimension "flavor"
            //url动态替换,正式url
            buildConfigField("String", "API_HOST", "${API_RELEASE_HOST}")
        }
        //开发
        flavors_dev {
            dimension "flavor"
            //url动态替换
            buildConfigField("String", "API_HOST", "${API_DEV_HOST}")
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation 'androidx.viewpager2:viewpager2:1.0.0-beta05'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    kapt 'androidx.room:room-compiler:2.2.0-alpha01'
    implementation 'androidx.room:room-rxjava2:2.2.0-alpha01'


    kapt 'com.google.dagger:dagger-android-processor:2.23.2'
    kapt 'com.google.dagger:dagger-compiler:2.23.2'

    implementation 'com.king.frame:mvvmframe:1.1.0'

    implementation 'com.king.base:adapter:3.2.1-androidx'
    implementation 'com.king.base:util:3.2.1-androidx'
    implementation 'com.king.anetty:anetty:1.0.1'
    //app-updater
    implementation 'com.king.app:app-updater:1.0.5-androidx'
    //app-dialog
    implementation 'com.king.app:app-dialog:1.0.5-androidx'

    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0+'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.49-androidx'

    implementation 'de.hdodenhof:circleimageview:3.0.1'

    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'com.zhihu.android:matisse:0.5.3-beta2'

    implementation 'top.zibin:Luban:1.1.8'

    implementation 'com.github.yalantis:ucrop:2.2.4'

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'

    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    implementation 'com.github.lzyzsd:jsbridge:1.0.4'

    implementation 'com.tencent:mmkv:1.0.23'



}
